apply plugin: 'java'

sourceSets.main.java.srcDir('src')

repositories {
	mavenCentral()
	maven { url "https://raw.githubusercontent.com/Zelaux/MindustryRepo/master/repository" }
	maven { url 'https://www.jitpack.io' }
}

java {
	targetCompatibility = JavaVersion.VERSION_17
	sourceCompatibility = JavaVersion.VERSION_17
}

ext {
	//windows sucks
	isWindows = System.getProperty("os.name").toLowerCase().contains("windows")
	sdkRoot = System.getenv("ANDROID_HOME") ?: System.getenv("ANDROID_SDK_ROOT")
}

tasks.withType(JavaCompile).configureEach {
	options.encoding = "UTF-8"
}

dependencies {
	compileOnly "com.github.Anuken.Arc:arc-core:$mindustryVersion"
	compileOnly "com.github.Anuken.Mindustry:core:$mindustryVersion"
	compileOnly "com.jakewharton.android.repackaged:dalvik-dx:11.0.0_r3"
	compileOnly files("D:/Projects/Template/libs/HeavyIndustryMod-Y0829A1.jar", "D:/Projects/Template/libs/android.jar", "D:/Projects/Template/libs/android-dalvik.jar")
}

jar {
	duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

task jarAndroid(type: Jar) {
	dependsOn "jar"

	archiveFileName = "${rootProject.name}Android.jar"

	from {
		[zipTree("${project(":android").buildDir}/libs/android.jar")]
	}

	doLast {
		if (!sdkRoot || !new File(sdkRoot).exists()) throw new GradleException("No valid Android SDK found. Ensure that ANDROID_HOME is set to your Android SDK directory.")

		def platformRoot = new File("$sdkRoot/platforms/").listFiles().sort().reverse().find { f -> new File(f, "android.jar").exists() }

		if (!platformRoot) throw new GradleException("No android.jar found. Ensure that you have an Android platform installed.")

		//collect dependencies needed for desugaring
		def dependencies = (configurations.compileClasspath.asList() + configurations.runtimeClasspath.asList() + [new File(platformRoot, "android.jar")]).collect { "--classpath $it.path" }.join(" ")

		def d8 = isWindows ? "d8.bat" : "d8"

		//dex and desugar files - this requires d8 in your PATH
		"$d8 $dependencies --min-api 14 --output ${rootProject.name}Android.jar ${rootProject.name}Android.jar"
				.execute(null, new File("$buildDir/libs")).waitForProcessOutput(System.out, System.err)
	}
}